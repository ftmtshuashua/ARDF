apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.lfp.androidrapiddevelopmentframework"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }

        manifestPlaceholders = [MTA_CHANNEL: "智者",
                                MTA_APPKEY : "AUFW43Z4G4VN"]
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', '../ardf/proguard-rules.pro'
        }

        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', '../ardf/proguard-rules.pro'


            /*包文件命名*/
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "ARDF_v${defaultConfig.versionName}_${defaultConfig.versionCode}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                }
            }
        }
    }
    lintOptions {
        checkReleaseBuilds true
        abortOnError true
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    //gradlew assembleRelease

    /*批量打包配置*/
    flavorDimensions "market"
    productFlavors {
        ARDF {
            dimension "market"
            manifestPlaceholders=[
                    MTA_CHANNEL:"智者"
            ]
        }
        HuaWei {
            dimension "market"
            manifestPlaceholders=[
                    MTA_CHANNEL:"华为"
            ]
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'jp.wasabeef:recyclerview-animators:2.3.0'
    implementation 'com.lfp.widget:SpringViewLibrary:1.1.3'
    implementation project(':ardf')
    implementation 'com.squareup.okhttp3:okhttp:3.2.0'
    implementation 'com.squareup.okio:okio:1.7.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.14-RC1'
    implementation 'com.artemzin.rxjava:proguard-rules:1.1.1.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    /*腾讯统计相关*/
    implementation 'com.tencent.mid:mid:3.73-release'
    implementation 'com.qq.mta:mta:3.3.1-release'
    //    implementation 'com.qq.visual:visual:3.3.1-release' 高级功能：可视化埋点
    implementation 'com.orhanobut:logger:2.2.0'
    implementation files('src/main/libs/GDTUnionSDK.TBS.4.16.564.min.jar')

    implementation 'com.yanzhenjie:permission:2.0.0-rc8'
}

//获取当前时间
def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}
